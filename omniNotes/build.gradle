/*
 * Copyright (C) 2015 Federico Iosue (federico.iosue@gmail.com)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {

    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId project.PACKAGE
        minSdkVersion project.MIN_SDK
        targetSdkVersion project.TARGET_SDK
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        vectorDrawables.useSupportLibrary = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    output.outputFile = new File(
                            (String) file.parent,
                            (String) file.name.replace("omniNotes", "Omni Notes-${variant.versionName}"))
                }
            }
        }
    }

    productFlavors {
        prod {}
        alpha {
            applicationId project.PACKAGE + ".alpha"
        }
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest {
            java.srcDirs = ['src/androidTest/java']
        }
    }
}

dependencies {
    androidTestCompile 'junit:junit:4.12'

    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.5.4'
    testCompile 'junit:junit:4.12'
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.2'
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.4@aar'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile('ch.acra:acra:4.8.5') {
        exclude group: 'org.json'
    }
    compile 'com.github.gabrielemariotti.changeloglib:changelog:2.0.0'
    compile 'be.billington.calendar.recurrencepicker:library:1.1.1'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.pushbullet:android-extensions:1.0.4@aar'
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'com.nhaarman.listviewanimations:lib-core:3.1.0@aar'
    compile 'com.nhaarman.listviewanimations:lib-manipulation:3.1.0@aar'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.6@aar') {
        transitive = true
    }
    compile('com.github.afollestad.material-dialogs:commons:0.8.5.6@aar') {
        transitive = true
    }
    compile 'com.jakewharton:butterknife:7.0.1'
    compile('org.mnode.ical4j:ical4j:1.0.6') {
        exclude group: 'commons.io'
    }
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'
    compile 'com.pnikosis:materialish-progress:1.5'
    compile 'com.github.paolorotolo:appintro:1.3.0'
    compile 'io.nlopez.smartlocation:library:3.2.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'com.artemzin.rxjava:proguard-rules:1.0.14.2'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.4.2@aar'
    compile 'org.ocpsoft.prettytime:prettytime:3.2.7.Final'
    compile 'org.piwik.sdk:piwik-sdk:0.0.4'
    compile 'org.bitbucket.cowwoc:diff-match-patch:1.1'

    compile 'com.github.federicoiosue:SimpleGallery:1.2.0'
    compile 'com.github.federicoiosue:Springpad-Importer:1.0.1'
    compile 'com.github.federicoiosue:Omni-Notes-Commons:1.1.2'
    compile 'com.github.federicoiosue:checklistview:3.1.3'
    compile 'com.github.federicoiosue:pixlui:2.6'
}

android.buildTypes.each { type ->
    type.buildConfigField 'String', 'ANALYTICS_URL', '"' + ANALYTICS_URL + '"'
    type.buildConfigField 'String', 'CRASH_REPORTING_URL', '"' + CRASH_REPORTING_URL + '"'
    type.buildConfigField 'String', 'VERSION_CHECK_URL', '"' + VERSION_CHECK_URL + '"'
}
